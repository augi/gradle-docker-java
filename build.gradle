plugins {
    id 'groovy'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'com.gradle.plugin-publish' version '1.0.0'
}

group 'cz.augi'
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation ('org.spockframework:spock-core:2.3-groovy-4.0') {
        exclude group:'org.codehaus.groovy'
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8

test {
    useJUnitPlatform()
    testLogging {
        events 'failed'
        exceptionFormat 'full'
    }
}

project.ext.set('gradle.publish.key', System.getenv('GRADLE_PORTAL_KEY'))
project.ext.set('gradle.publish.secret', System.getenv('GRADLE_PORTAL_SECRET'))

pluginBundle {
    website = 'https://github.com/augi/gradle-docker-java'
    vcsUrl = 'https://github.com/augi/gradle-docker-java'

    tags = ['docker', 'java', 'jvm']
}

gradlePlugin {
    plugins {
        dockerJavaPlugin {
            id = 'cz.augi.docker-java'
            displayName = 'Gradle Docker Java plugin'
            description = 'Gradle plugin that wraps your JVM application to a new Docker image.'
            implementationClass = 'cz.augi.gradle.dockerjava.DockerJavaPlugin'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

afterEvaluate {
    publishing.publications.forEach {
        it.pom {
            name = 'Gradle Docker Java Plugin'
            description = 'Gradle plugin that wraps your JVM application to a new Docker image.'
            url = 'https://github.com/augi/gradle-docker-java'
            licenses {
                license {
                    name = 'The MIT License'
                    url = 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            developers {
                developer {
                    id = 'augi'
                    name = 'Michal August√Ωn'
                    email = 'augustyn.michal@gmail.com'
                }
            }
            scm {
                connection = 'scm:git:git://github.com/augi/gradle-docker-java.git'
                developerConnection = 'scm:git:ssh://github.com/augi/gradle-docker-java.git'
                url = 'https://github.com/augi/gradle-docker-java'
            }
        }
    }
}

signing {
    String base64Key = System.getenv('SIGNING_KEY')
    if (base64Key) {
        useInMemoryPgpKeys(new String(Base64.decoder.decode(base64Key)), System.getenv('SIGNING_PASSWORD'))
        sign publishing.publications
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}
